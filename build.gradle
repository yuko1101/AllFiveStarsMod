buildscript {
    repositories {
        maven {
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath "com.github.yuko1101:LunarCoreRunner:128657bd1f"
    }
}

plugins {
    id "java"
}

apply plugin: "io.github.yuko1101.lunarcorerunner"

version = project.mod_version
group = project.maven_group

repositories {
    maven {
        url = "https://maven.fabricmc.net/"
    }
    maven {
        url = "https://repo.spongepowered.org/repository/maven-public"
    }
    maven {
        url = "https://jitpack.io"
    }
}

dependencies {
    // TODO: use gameprovider_version
    gameprovider "com.github.yuko1101:LunarCoreGameProvider:26ac46d126"

    // TODO: use lunarcore_version
    lunarcore "com.github.Melledy:LunarCore:development-SNAPSHOT"

    implementation "org.spongepowered:mixin:${project.mixin_version}"
    annotationProcessor "org.spongepowered:mixin:${project.mixin_version}:processor"

    implementation "io.github.llamalad7:mixinextras-common:0.3.5"
}

processResources {
    inputs.property "version", project.version
    inputs.property "lunarcore_version", project.lunarcore_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version, "lunarcore_version": project.lunarcore_version, "loader_version": project.loader_version
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}